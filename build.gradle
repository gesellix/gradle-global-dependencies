repositories {
  mavenLocal()
  mavenCentral()
}

apply plugin: 'groovy'

def versionOverrides = [
    'org.yaml:snakeyaml'        : '1.10',
    'org.hamcrest:hamcrest-core': '1.3'
]

dependencies {
  compile gradleApi()
  compile localGroovy()

  compile 'org.yaml:snakeyaml:1.10'

  testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
}

configurations.all {
  resolutionStrategy {
    failOnVersionConflict()
//    force versionOverrides.collect { k, v -> "$k:$v" }

    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
      def overrideVersion = versionOverrides[details.requested.group + ":" + details.requested.name]
      if (overrideVersion != null && details.requested.version != overrideVersion) {
        logger.lifecycle "Overriding dependency ${details.requested.group}:${details.requested.name} version ${details.requested.version} --> $overrideVersion"
        details.useVersion overrideVersion
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.12-rc-2'
  distributionUrl = 'http://services.gradle.org/distributions/gradle-1.12-rc-2-all.zip'
}
